#@TYPE: Machine
#@NAME: TQ-Group TQMa7x Module on MBa7 Baseboard
#@SOC: i.MX7
#@DESCRIPTION: Machine configuration for TQ-Group TQMa7 SOLO / DUAL
#@MAINTAINER: Markus Niebel <Markus.Niebel@tq-group.com>
#
# By default, this machine will build for the full featured option
# with SD / eMMC boot
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor           Memory  Boot device  Configuration
# --------------      ------  -----------  --------------------
# i.MX7 S/D	      512MiB  MMC / SD     tqma7d_512mb_mba7_mmc_defconfig
# i.MX7 S/D	      512MiB  QSPI NOR     tqma7d_512mb_mba7_qspi_defconfig
# i.MX7 S/D	      1GiB    MMC / SD     tqma7d_1gb_mba7_mmc_defconfig
# i.MX7 S/D	      1GiB    QSPI NOR     tqma7d_1gb_mba7_qspi_defconfig
#

include conf/machine/include/tqma7.inc

SERIAL_CONSOLE = "115200 ttymxc5"

KERNEL_DEVICETREE ?= " \
                        imx7d-mba7.dtb \
                        imx7d-mba7-lvds.dtb \
                        imx7d-mba7-otg.dtb \
"

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL_append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES ?= "\
	${KERNEL_IMAGETYPE};linuximage \
	${KERNEL_IMAGETYPE}-imx7d-mba7.dtb;imx7d-mba7.dtb \
	${KERNEL_IMAGETYPE}-imx7d-mba7-lvds.dtb;imx7d-mba7-lvds.dtb \
	${KERNEL_IMAGETYPE}-imx7d-mba7-otg.dtb;imx7d-mba7-otg.dtb \
	"

WKS_FILE ?= "tqmaxx-sdimg.wks"

UBOOT_CONFIG ??= "sd sd_512mb sd_1gb qspi_512mb qspi_1gb"
UBOOT_CONFIG[sd] = "tqma7d_512mb_mba7_mmc_defconfig"
UBOOT_CONFIG[sd_512mb] = "tqma7d_512mb_mba7_mmc_defconfig"
UBOOT_CONFIG[sd_1gb] = "tqma7d_1gb_mba7_mmc_defconfig"
UBOOT_CONFIG[qspi_512mb] = "tqma7d_512mb_mba7_qspi_defconfig"
UBOOT_CONFIG[qspi_1gb] = "tqma7d_1gb_mba7_qspi_defconfig"
UBOOT_CONFIG[mfgtool] = "tqma7d_512mb_mba7_mmc_defconfig"

